apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle){
    source 'src'
    include '**/*.java'
    // code from Google/AOSP
    exclude 'main/java/org/kontalk/util/ThumbnailUtils.java',
        'main/java/org/kontalk/crypto/PRNGFixes.java',
        'main/java/org/kontalk/ui/AccountAuthenticatorActionBarActivity.java',
        'main/java/org/kontalk/billing/*.java',
        'googleplay/java/org/kontalk/billing/GoogleBillingService.java',
        'googleplay/java/org/kontalk/billing/Inventory.java',
        'googleplay/java/org/kontalk/billing/ProductDetails.java',
        'googleplay/java/org/kontalk/billing/Purchase.java'
    // 3rd-party code
    exclude '**/IconContextMenu.java', '**/CircularSeekBar.java',
        '**/KeyboardAwareFrameLayout.java'
    classpath = files()
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        applicationId "org.kontalk"
        resValue "string", "account_type", applicationId + '.account'
        resValue "string", "user_itemtype", "vnd.android.cursor.item/org.kontalk.user"
        buildConfigField "String", "ACCOUNT_TYPE", '"org.kontalk.account"'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
        }
        debug {
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    productFlavors {
        basic {
        }
        googleplay {
        }
        tigase {
            applicationId "org.kontalk.tigase"
            resValue "string", "account_type", applicationId + '.account'
            resValue "string", "user_itemtype", "vnd.android.cursor.item/org.kontalk.tigase.user"
            buildConfigField "String", "ACCOUNT_TYPE", '"org.kontalk.tigase.account"'
        }
    }

}

dependencies {
    compile (project(':client-common-java')) {
        exclude group: 'net.sf.kxml'
    }
    compile project(':emojicon:library')

    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile 'com.madgag.spongycastle:prov:1.51.0.0'
    compile 'com.madgag.spongycastle:pkix:1.51.0.0'
    compile 'com.madgag.spongycastle:pg:1.51.0.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:6.2.2'
    compile 'dnsjava:dnsjava:2.1.6'
    compile 'com.jcraft:jzlib:1.1.3'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.jcraft:jzlib:1.1.3'
    compile 'org.igniterealtime.smack:smack-android:4.1.0-alpha6-SNAPSHOT'
    compile 'org.igniterealtime.smack:smack-core:4.1.0-alpha6-SNAPSHOT'
    compile 'org.igniterealtime.smack:smack-tcp:4.1.0-alpha6-SNAPSHOT'
    compile 'org.igniterealtime.smack:smack-extensions:4.1.0-alpha6-SNAPSHOT'

    googleplayCompile 'com.google.android.gms:play-services:5.0.89'
}
