apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle){
    source 'src'
    include '**/*.java'
    // code from Google/AOSP
    exclude 'main/java/org/kontalk/util/ThumbnailUtils.java',
        'main/java/org/kontalk/crypto/PRNGFixes.java',
        'main/java/org/kontalk/ui/AccountAuthenticatorActionBarActivity.java',
        'main/java/org/kontalk/billing/*.java',
        '*/java/org/kontalk/billing/GoogleBillingService.java',
        '*/java/org/kontalk/billing/Inventory.java',
        '*/java/org/kontalk/billing/ProductDetails.java',
        '*/java/org/kontalk/billing/Purchase.java'
    // 3rd-party code
    exclude '**/IconContextMenu.java', '**/CircularSeekBar.java',
        '**/KeyboardAwareRelativeLayout.java'
    classpath = files()
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "org.kontalk"
        resValue "string", "account_type", applicationId + '.account'
        resValue "string", "user_itemtype", "vnd.android.cursor.item/org.kontalk.user"
        buildConfigField "String", "ACCOUNT_TYPE", '"org.kontalk.account"'
    }

    if (project.hasProperty('RELEASE_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
            if (project.hasProperty('RELEASE_STORE_FILE'))
                signingConfig signingConfigs.release
        }
        debug {
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    productFlavors {
        basic {
        }
        googleplay {
        }
    }

}

dependencies {
    compile (project(':client-common-java')) {
        exclude group: 'net.sf.kxml'
    }
    compile project(':emojicon:library')

    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile 'com.madgag.spongycastle:prov:1.51.0.0'
    compile 'com.madgag.spongycastle:pkix:1.51.0.0'
    compile 'com.madgag.spongycastle:pg:1.51.0.0'
    compile 'com.googlecode.libphonenumber:libphonenumber:7.0.2'
    compile 'dnsjava:dnsjava:2.1.6'
    compile 'com.jcraft:jzlib:1.1.3'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.jcraft:jzlib:1.1.3'
    compile "org.igniterealtime.smack:smack-android:$smackVersion"
    compile "org.igniterealtime.smack:smack-core:$smackVersion"
    compile "org.igniterealtime.smack:smack-tcp:$smackVersion"
    compile "org.igniterealtime.smack:smack-extensions:$smackVersion"
    compile "org.igniterealtime.smack:smack-experimental:$smackVersion"
    compile "org.igniterealtime.smack:smack-android-extensions:$smackVersion"

    googleplayCompile 'com.google.android.gms:play-services-base:6.5.87'

    testCompile 'junit:junit:4.+'
}
