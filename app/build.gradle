apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle){
    source 'src'
    include '**/*.java'
    // code from Google/AOSP
    exclude 'main/java/org/kontalk/util/ThumbnailUtils.java',
        'main/java/org/kontalk/crypto/PRNGFixes.java',
        'main/java/org/kontalk/ui/AccountAuthenticatorActionBarActivity.java',
        'main/java/org/kontalk/billing/*.java',
        '*/java/org/kontalk/billing/GoogleBillingService.java',
        '*/java/org/kontalk/billing/Inventory.java',
        '*/java/org/kontalk/billing/ProductDetails.java',
        '*/java/org/kontalk/billing/Purchase.java'
    // 3rd-party code
    exclude '**/IconContextMenu.java', '**/CircularSeekBar.java',
        '**/KeyboardAwareRelativeLayout.java', '**/FrameLayoutFixed.java',
        '**/NoCacheMiniDnsResolver.java'
    def configProps = ['baseDir': projectDir.absolutePath]
    configProperties configProps
    classpath = files()
}

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "org.kontalk"
        versionCode 60
        versionName "3.0.4"
        targetSdkVersion 22
        resValue "string", "account_type", applicationId + '.account'
        buildConfigField "String", "ACCOUNT_TYPE", '"org.kontalk.account"'
    }

    if (project.hasProperty('RELEASE_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
            if (project.hasProperty('RELEASE_STORE_FILE'))
                signingConfig signingConfigs.release
        }
        debug {
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    productFlavors {
        basic {
            minSdkVersion 8
        }
        googleplay {
            minSdkVersion 9
        }
    }

}

dependencies {
    compile (project(':client-common-java')) {
        exclude group: 'net.sf.kxml'
    }
    compile project(':emojicon:library')

    compile 'com.android.support:appcompat-v7:20.0.0'
    compile "com.madgag.spongycastle:core:$spongyCastleVersion"
    compile "com.madgag.spongycastle:prov:$spongyCastleVersion"
    compile "com.madgag.spongycastle:pkix:$spongyCastleVersion"
    compile "com.madgag.spongycastle:pg:$spongyCastleVersion"
    compile 'com.googlecode.libphonenumber:libphonenumber:7.0.7'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.google.zxing:core:3.2.0'
    compile 'com.segment.backo:backo:1.0.0'
    compile "org.igniterealtime.smack:smack-experimental:$smackVersion"
    compile "org.igniterealtime.smack:smack-android-extensions:$smackVersion"

    googleplayCompile 'com.google.android.gms:play-services-base:6.5.87'

    testCompile 'junit:junit:4.+'
}
