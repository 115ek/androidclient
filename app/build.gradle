apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

check.dependsOn 'checkstyle'

task checkstyle(type: Checkstyle){
    source 'src'
    include '**/*.java'
    // code from Google/AOSP
    exclude 'main/java/org/kontalk/util/ThumbnailUtils.java',
        'main/java/org/kontalk/crypto/PRNGFixes.java',
        'main/java/org/kontalk/ui/AccountAuthenticatorActionBarActivity.java',
        'main/java/org/kontalk/billing/*.java',
        '*/java/org/kontalk/billing/GoogleBillingService.java',
        '*/java/org/kontalk/billing/Inventory.java',
        '*/java/org/kontalk/billing/ProductDetails.java',
        '*/java/org/kontalk/billing/Purchase.java'
    // 3rd-party code
    exclude '**/CircularSeekBar.java',
        '**/KeyboardAwareRelativeLayout.java', '**/FrameLayoutFixed.java',
        '**/AbsListViewScrollDetector.java', '**/NoCacheMiniDnsResolver.java',
        '**/XMPPTCPConnection.java',
        'main/java/com/**'
    def configProps = ['baseDir': projectDir.absolutePath]
    configProperties configProps
    classpath = files()
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "org.kontalk"
        versionCode 78
        versionName "3.1.1"
        targetSdkVersion 22
        resConfigs "en", "de", "fr", "it", "es", "ca", "cs", "el", "fa", "gl", "ja", "nl", "pt", "ru", "sr", "zh-rCN"
        resValue "string", "account_type", applicationId + '.account'
        buildConfigField "String", "ACCOUNT_TYPE", '"org.kontalk.account"'
    }

    if (project.hasProperty('RELEASE_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
            if (project.hasProperty('RELEASE_STORE_FILE'))
                signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled true
            proguardFile 'proguard.cfg'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    productFlavors {
        basic {
            minSdkVersion 8
        }
        googleplay {
            minSdkVersion 9
        }
    }

}

dependencies {
    compile (project(':client-common-java')) {
        exclude group: 'net.sf.kxml'
    }
    compile project(':emojicon:library')

    compile "com.android.support:appcompat-v7:$appcompatVersion"
    compile "com.android.support:design:$appcompatVersion"
    compile "com.madgag.spongycastle:core:$spongyCastleVersion"
    compile "com.madgag.spongycastle:prov:$spongyCastleVersion"
    compile "com.madgag.spongycastle:pkix:$spongyCastleVersion"
    compile "com.madgag.spongycastle:pg:$spongyCastleVersion"
    compile 'com.googlecode.libphonenumber:libphonenumber:7.1.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.google.zxing:core:3.2.1'
    compile 'com.segment.backo:backo:1.0.0'
    compile "org.igniterealtime.smack:smack-experimental:$smackVersion"
    compile "org.igniterealtime.smack:smack-android-extensions:$smackVersion"
    compile 'com.github.machinarius:preferencefragment:0.1.1'
    compile('com.afollestad.material-dialogs:core:0.8.5.0@aar') {
        transitive = true
    }
    compile('com.afollestad.material-dialogs:commons:0.8.5.0@aar') {
        transitive = true
    }
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile 'com.nispok:snackbar:2.11.0'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.github.vlivanov:ListViewVariants:f606578467'

    googleplayCompile "com.google.android.gms:play-services-base:$googlePlayServicesVersion"
    googleplayCompile "com.google.android.gms:play-services-gcm:$googlePlayServicesVersion"

    testCompile 'junit:junit:4.+'
}
